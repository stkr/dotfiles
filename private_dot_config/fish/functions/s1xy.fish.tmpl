function _s1xy_init
    set -g -x JIRA_PROJECT "ESE"
    set -g -x BASE_PATH "/home/$USER/projects/s1xy"
    set -g -x LLVM_BIN_PATH $BASE_PATH/src/riscv32-nxp-elf_0.7.2_cssi/bin

{{ if contains "nxdi.us-aus" .chezmoi.fqdnHostname }}
    # from /home/mcu_zebu_scr1/stec.seco/setup_nxdi/setup.bash
    set -g -x TRPOLICYFILE "/home/mcu_zebu_scr1/stec.seco/setup_nxdi/tr_policy"
    set -g -x PATH "/home/mcu_zebu_scr1/stec.seco/setup_nxdi/bin:$PATH"

    set -g -x LD_LIBRARY_PATH "$LD_LIBRARY_PATH:/pkg/TWW-python-/3.9.13/x86_64-linux3.10-glibc2.17/lib"
    set -g -x LD_LIBRARY_PATH "$LD_LIBRARY_PATH:/home/nxf51249/stec/external/lib64"
    set -g -x LD_LIBRARY_PATH "$LD_LIBRARY_PATH:/pkg/OSS-openssl-/3.1.0/x86_64-linux/lib64"
{{ end }}

    if test -e "$BASE_PATH/.venv/bin/activate.fish"
        source "$BASE_PATH/.venv/bin/activate.fish"
    end
end

function _s1xy_cd
    if test -z "$JIRA_PROJECT"
        _s1xy_init
    end

    cd "$BASE_PATH/src/s1xy"
end

function _s1xy_edit
    _s1xy_cd
    nvim -S .session.vim
end

function _s1xy_branch -a nr -a text
    if test -z "$JIRA_PROJECT"
        _s1xy_init
    end

    if test (count $argv) -lt 2
        echo "Not enough arguments. Usage: branch NR TEXT."
        return 1
    end

    set -l owner "sk"
    set -l branch "$owner/$JIRA_PROJECT-$nr-$text"
    
    git checkout -b "$branch"; or return
    git push --set-upstream origin "$branch"; or return
end

function _s1xy_bininfo -a objectfile
    if test -z "$JIRA_PROJECT"
        _s1xy_init
    end

    if test (count $argv) -lt 1
        echo "Not enough arguments. Usage: bininfo FILE."
        return 1
    end

    set -l objdump "$LLVM_BIN_PATH/llvm-objdump"
    if ! test -e "$objdump"
        echo "Objdump $objdump does not exist"
        return 1
    end

    $objdump --disassemble $argv[1] > "$argv[1].dis"; or return
    echo "disassembly written to [$argv[1].dis]"
    $objdump --syms "$argv[1]" | sort > "$argv[1].sym"; or return
    echo "symbol table written to [$argv[1].sym]"
end

function s1xy -d "Functions specific to s1xy project"
    # We tell argparse about -h/--help and others -these are short and long
    # forms of the same option. The "--" here is mandatory, it tells it from
    # where to read the arguments.
    argparse h/help -- $argv; or return

    # If -h or --help is given, we print a little help text and return
    if set -ql _flag_help || test (count $argv) -lt 1
        echo "Usage:                                                     "
        echo "    s1xy command                                           "
        echo "                                                           "
        echo "Valid commands:                                            "
        echo "    * init: Initialize the environment (incl. python venv)."
        echo "    * cd: Iniialize the environment and change to the      "
        echo "      source directory.                                    "
        echo "    * edit: Initialize the environment and launch nvim in  "
        echo "      the source directory.                                "
        echo "    * branch: Create a branch from the currently checked   "
        echo "      out commit. Takes an artifact number and a short     "
        echo "      description as arguments.                            "
        echo "    * bininfo: Extract disassembly and symbol file from    "
        echo "      binary.                                              "
        return 0
    end

    switch $argv[1]
        case init
            _s1xy_init
        case cd
            _s1xy_cd
        case edit
            _s1xy_edit
        case branch
            _s1xy_branch $argv[2..-1]
        case bininfo
            _s1xy_bininfo $argv[2..-1]
        case '*'
            echo Unsupported command \"$argv[1]\"
            return 1
    end
end

set -l s1xy_commands init cd edit branch bininfo
complete -c s1xy -f -s h -l help
complete -c s1xy -f -n "not __fish_seen_subcommand_from $s1xy_commands" -a "init" -d "Initialize the environment (incl. python venv)."
complete -c s1xy -f -n "not __fish_seen_subcommand_from $s1xy_commands" -a "cd" -d "Iniialize the environment and change to the source directory."
complete -c s1xy -f -n "not __fish_seen_subcommand_from $s1xy_commands" -a "edit" -d "Initialize the environment and launch nvim in the source directory."
complete -c s1xy -f -n "not __fish_seen_subcommand_from $s1xy_commands" -a "branch" -d "Create a branch from the currently checked out commit."
complete -c s1xy -n "not __fish_seen_subcommand_from $s1xy_commands" -a "bininfo" --force-files -d "Extract disassembly and symbol file from binary."
